// PEG Grammar which can be used interactively to create a parser at https://pest.rs/#editor

// This grammar concept represents the idea where twig syntax is allowed inside html.
// It does not contain every possible html tag or twig syntax

// accepted language example
//
// {% block my_block %}
// <div class="my-block-div {{ additional_class }}">
//     <div {{ some_attribute }}/>
//     <p {% block if_block %}
//            style="color: red"
//        {% endblock %}
//     >
//          hello {{ world }}
//     </p>
// </div>
// {% endblock %}
//
// But it has one serious flaw!
// It allows HTML tags inside Twig blocks inside HTML opening tags
// and wrongly parses html attributes inside top level Twig blocks instead of parsing them as HTML text.

// any element in the document (including twig structures)
element = { ws? ~ (twig | any_tag | text) }

// twig definitions
twig = { twig_structure | twig_expression }
twig_structure = { twig_block }
twig_expression = { "{{" ~ ws? ~ text ~ ws? ~ "}}" }

// twig structure definitions
inner_twig = { inner_tag | element }
twig_block = { "{%" ~ ws? ~ "block" ~ ws? ~ name ~ ws? ~ "%}"
                ~ ws? ~ inner_twig* ~ ws?
                ~ "{%" ~ ws? ~ "endblock" ~ ws? ~ "%}" }
// more structures like for loops or if conditions...

// tag definitions
any_tag = { div_tag | p_tag }
div_tag = { "<div" ~ ws? ~ inner_tag* ~ ("/>" | ">"
            ~ children? ~ ws?
            ~ "</div>") }
p_tag = { "<p" ~ ws? ~ inner_tag* ~ ("/>" | ">"
            ~ children? ~ ws?
            ~ "</p>") }
// more html tags...

inner_tag = { (attribute | twig) ~ ws? }
// tag children and attribute definitions
children = { element+ ~ ws? }
attribute = { name ~ "=" ~ string }

// general text and whitespace
name = { ('a'..'z' | 'A'..'Z' | '0'..'9' | "_" | "-" | ":" | "@" | "#")+ }
text = { (!(ws | "<" | "{{" | "{%") ~ ANY)+ }
ws = { (" " | "\t" | "\n")+ }

// quoted "strings", which can include twig
string = { "\"" ~ (raw_string | escape | twig)* ~ "\"" }
raw_string = { (!("\\" | "\"" | "{{" | "{%") ~ ANY)+ }
predefined = { "n" | "r" | "t" | "\\" | "0" | "\"" | "'" }
escape     = { "\\" ~ (predefined) }
